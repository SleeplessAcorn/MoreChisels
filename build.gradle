buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mc_version}-${mod_version}"
if (mod_build != "")
    version = "${mc_version}-${mod_version}-${mod_build}"
group = "${mod_domain}.${mod_id}"
archivesBaseName = mod_id

sourceCompatibility = compat_version
targetCompatibility = compat_version

libsDirName = build_dir
distsDirName = build_dir

compileJava {
    sourceCompatibility = compat_version
    targetCompatibility = compat_version
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = run_dir
    mappings = mcp_mappings

    replace '%mod_version%', "${mod_version}"
    replace '%mc_version%', "${mc_version}"
    replace '%chisel_version%', "MC${mc_version}-${chisel_version}"

    replace 'scala.actors.threadpool.Arrays', 'java.util.Arrays'

    useDepAts = true
    makeObfSourceJar = true
}

repositories {
    maven { url = "http://maven.tterrag.com/" }
}

dependencies {
    runtime "team.chisel.ctm:CTM:MC${mc_version}-${ctm_version}"
    runtime "team.chisel:Chisel:MC${mc_version}-${chisel_version}"

    compile "team.chisel:Chisel:MC${mc_version}-${chisel_version}:api"
}

processResources {
    inputs.property "build_version", "${mod_version}-${mod_build}"
    inputs.property "mcf_version", "${mc_version}-${forge_version}"

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand  'build_version': "${mod_version}-${mod_build}",
                'mcf_version': "${mc_version}-${forge_version}"
    }

    from(sourceSets.main.resources.srcDirs) {exclude 'mcmod.info'}
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {}
}

artifacts {
    archives deobfJar
}